bcview {
    // brush tools
    AdditiveBrush {
        - Tool
        - AdditiveBrush {value: 1}
        - BrushSize {radius: 1}
        - (BrushField, bc.planes.CellData.height)
    }

    ValueBrush {
        - Tool
        - ValueBrush {value: 1}
        - BrushSize {radius: 1}
        - (BrushField, bc.planes.CellData.height)
    }

    RiverBrush {
        - Tool
        - RiverBrush {value: 1}
    }

    PrefabBrush {
        - Tool
        - PrefabBrush {prefab: 0}
    }

    // action bindings
    actions {
        using components.input
        using components.input.Key
        using bcview.systems.input
        with (ActionGroup, cameraKeyboard) {
            camera_pan {
                - ActionVector {system: CameraPan}
                - (InputVector, E) {y: 1}
                - (InputVector, D) {y: -1}
                - (InputVector, S) {x: 1}
                - (InputVector, F) {x: -1}
            }
            camera_orbit {
                - ActionVector {system: CameraOrbit}
                - (InputVector, R) {x: 1}
                - (InputVector, W) {x: -1}
                - (InputVector, T) {y: 1}
                - (InputVector, G) {y: -1}
            }
            camera_zoom {
                - ActionVector {system: CameraZoom}
                - (InputVector, X) {y: 1}
                - (InputVector, Z) {y: -1}
            }

            recenter {
                - ActionButton {system: CameraToPlayer}
                - (ButtonDown, C)
            }
        }
        with (ActionGroup, cameraMouse) {
            camera_pan_mouse {
                - ActionVector {system: CameraPan}
                - (InputVector, Mouse.Delta) {0.1, 0.1}
                - Disabled
            }
            camera_pan_mouse_start {
                - ActionButton {system: systems.input.EcsEnable}
                - (ButtonDown, Mouse.Left)
                - (ActionParams, flecs.meta.entity) {camera_pan_mouse}
            }
            camera_pan_mouse_end {
                - ActionButton {system: systems.input.EcsDisable}
                - (ButtonUp, Mouse.Left)
                - (ActionParams, flecs.meta.entity) {camera_pan_mouse}
            }
        }
    }
}
